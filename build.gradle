plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'java'
apply plugin: 'maven'

group 'me.dags'
version '1.8.0-SNAPSHOT'
def spongeAPI = '5.2.0'
def spongeChannel = 'SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeAPI}-$spongeChannel"
    compile 'io.github.lukehutch:fast-classpath-scanner:2.0.16'
    compile 'com.github.dags-:Fmt:0.1.1'
}

shadowJar {
    dependencies {
        include dependency('io.github.lukehutch:fast-classpath-scanner')
        include dependency('com.github.dags-:Fmt')
    }
    relocate 'io.github.lukehutch.fastclasspathscanner', 'me.dags.commandbus.fcps'
    relocate 'me.dags.fmt', 'me.dags.commandbus.fmt'
    classifier = null
}

jar.dependsOn shadowJar
jar.enabled = false

// exclude dependencies since they're shadowed into the jar already
install.repositories.mavenInstaller.pom.whenConfigured { pom ->
    pom.dependencies.removeAll { it.scope == "compile" }
}
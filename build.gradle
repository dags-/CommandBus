plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.0'
    id 'java'
    id 'maven'
}

group 'me.dags'
version '2.0.3'
def spongeAPI = '7.1.0'
def spongeChannel = 'SNAPSHOT'

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven { url 'http://repo.spongepowered.org/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeAPI}-$spongeChannel"
    shade 'com.github.dags-:Command:0.1.3'
    shade 'com.github.dags-:Fmt:0.2.0'
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'me.dags.command.', 'me.dags.commandbus.'
    relocate 'me.dags.fmt.', 'me.dags.commandbus.fmt.'
    classifier = null
}

install.repositories.mavenInstaller.pom.whenConfigured { pom ->
    pom.dependencies.removeAll { it.scope == "compile" }
}

jar.dependsOn shadowJar
jar.enabled = false
